buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0" // ... for packaging into a JAR
        classpath "com.github.gmazzo:gradle-buildconfig-plugin:2.0.2" // for "BuildConfig" object generation
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.github.gmazzo.buildconfig'
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = "jaaska.jaakko.photosapp.server.ApplicationKt"
group 'jaaska.jaakko'
version "$appVersionName-$appVersionCode"

// BuildConfig with https://github.com/gmazzo/gradle-buildconfig-plugin .
buildConfig {
    buildConfigField("String", "versionName", "\"$appVersionName\"")
    buildConfigField("Int", "versionCode", "$appVersionCode")
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src', 'build/generated']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"

    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-serialization:$ktor_version"

    implementation "org.xerial:sqlite-jdbc:3.32.3.2" // SQLite DB driver

    implementation "net.coobird:thumbnailator:0.4.3" // For thumbnails
    implementation "com.drewnoakes:metadata-extractor:2.14.0" // For EXIF

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
}

shadowJar {
    // archiveBaseName.set("photosapp-server") // TODO If this is uncommented, outputs two JARs. See what's up with that
    archiveClassifier.set("")
    archiveVersion.set("$project.version")
}
